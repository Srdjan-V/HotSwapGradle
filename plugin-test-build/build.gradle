plugins {
    id 'java'
    id 'application'
    id 'io.github.srdjan-v.hotswap-gradle'
}

application {
    mainClass = 'Main'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

def run11 = registerTask("run11")
def run17 = registerTask("run17")

hotSwap {
    configureTask(tasks.named("run"))
    configureTask(run11) {
        languageVersion = JavaLanguageVersion.of(11)
    }
    configureTask(run17) {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

TaskProvider<JavaExec> registerTask(String name) {
    def defaultTaskProvider = tasks.named("run", JavaExec)
    TaskProvider<JavaExec> runTask = tasks.register(name, JavaExec)
    runTask.configure {
        def defaultTask = defaultTaskProvider.get()
        setGroup(defaultTask.getGroup())

        setClasspath(defaultTask.getClasspath())
        getMainModule().set(application.getMainModule())
        getMainClass().set(application.getMainClass())
        getJvmArguments().convention(defaultTask.getJvmArgs())
    }
    return runTask
}
